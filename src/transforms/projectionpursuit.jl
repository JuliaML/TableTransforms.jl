# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    ProjectionPursuit(;tol=1e-6, maxiter=100, deg=5, perc=.9, n=100)

The projection pursuit multivariate transform converts any multivariate distribution into
the standard multivariate Gaussian distribution.

This iterative algorithm repeatedly finds a direction of projection `Î±` that maximizes a score of
non-Gaussianity known as the projection index `I(Î±)`. The samples projected along `Î±` are then
transformed with the [`Quantile`](@ref) transform to remove the non-Gaussian structure. The
other coordinates in the rotated orthonormal basis `Q = [Î± ...]` are left untouched.

The non-singularity of `Q` is controlled by assuring that `norm(det(Q)) â‰¥ tol`. The iterative 
process terminates whenever the transformed samples are "more Gaussian" than `perc`% of `n`
randomly generated samples from the standard multivariate Gaussian distribution, or when the 
number of iterations reaches a maximum `maxiter`.

# Examples

```julia
ProjectionPursuit()
ProjectionPursuit(deg=10)
ProjectionPursuit(perc=.85, n=50)
ProjectionPursuit(tol=1e-4, maxiter=250, deg=5, perc=.95, n=100)
```

See [https://doi.org/10.2307/2289161](https://doi.org/10.2307/2289161) for 
further details.
"""
struct ProjectionPursuit{T} <: StatelessFeatureTransform
  tol::T
  maxiter::Int
  deg::Int
  perc::T
  n::Int
end

ProjectionPursuit(;tol=1e-6, maxiter=100, deg=5, perc=.9, n=100) =
  ProjectionPursuit{typeof(tol)}(tol, maxiter, deg, perc, n)

isrevertible(::Type{<:ProjectionPursuit}) = true

# transforms a row of random variables into a convex combination 
# of random variables with values in [-1,1] and standard normal distribution
rscore(Z, Î±) = 2 .* cdf.(Normal(), Z * Î±) .- 1

# projection index of sample along a given direction
function pindex(transform, Z, Î±)
  d = transform.deg
  r = rscore(Z, Î±)
  I = (3/2) * mean(r)^2
  if d > 1
    Pâ±¼â‚‹â‚‚, Pâ±¼â‚‹â‚ = ones(length(r)), r
    for j = 2:d
      Pâ±¼â‚‹â‚‚, Pâ±¼â‚‹â‚ = 
        Pâ±¼â‚‹â‚, (1/j) * ((2j-1) * r .* Pâ±¼â‚‹â‚ - (j-1) * Pâ±¼â‚‹â‚‚)
      I += ((2j+1)/2) * (mean(Pâ±¼â‚‹â‚))^2
    end
  end
  I
end

# j-th element of the canonical basis in â„áµˆ
basis(d, j) = float(1:d .== j)

# index for all vectors in the canonical basis
function pbasis(transform, Z)
  q = size(Z, 2)
  [pindex(transform, Z, basis(q, j)) for j in 1:q]
end

# projection index of the standard multivariate Gaussian
function gaussquantiles(transform, N, q)
  n = transform.n
  p = 1.0 - transform.perc
  Is = [pbasis(transform, randn(N, q)) for i in 1:n]
  I  = reduce(hcat, Is)
  quantile.(eachrow(I), p)
end

function alphaguess(transform, Z)
  q = size(Z, 2)
  
  # objective function
  func(Î±) = pindex(transform, Z, Î±)
  
  # evaluate objective along axes
  j = argmax(j -> func(basis(q, j)), 1:q)
  Î± = basis(q, j)
  I = func(Î±)
  
  # evaluate objective along diagonals
  diag(Î±, s, e) = (1/âˆš(2+2s*Î±â‹…e)) * (Î± + s * e)
  for eáµ¢ in basis.(q, 1:q)
    dâ‚Š = diag(Î±, +1, eáµ¢)
    dâ‚‹ = diag(Î±, -1, eáµ¢)
    fâ‚Š = func(dâ‚Š)
    fâ‚‹ = Î±â‹…eáµ¢ != 1.0 ? func(dâ‚‹) : 0.0
    f, d = fâ‚Š > fâ‚‹ ? (fâ‚Š, dâ‚Š) : (fâ‚‹, dâ‚‹)
    if f > I
      Î± = d
      I = f
    end
  end
  
  Î±
end

function neldermead(transform, Z, Î±â‚€)
  f(Î±) = -pindex(transform, Z, Î± ./ norm(Î±))
  res = optimise(f, Î±â‚€, 1, xtol_rel=10eps())
  first(res)
end

function alphamax(transform, Z)
  Î± = alphaguess(transform, Z)
  neldermead(transform, Z, Î±)  
end

function orthobasis(Î±, tol)
  q = length(Î±)
  Q, R = qr([Î± rand(q,q-1)])
  while norm(diag(R)) < tol
    Q, R = qr([Î± rand(q,q-1)])
  end  
  Q
end

function rmstructure(transform, Z, Î±)
  # find orthonormal basis for rotation
  Q = orthobasis(Î±, transform.tol)

  # remove structure of first rotated axis
  newtable, qcache = apply(Quantile(1), Tables.table(Z * Q))
  
  # undo rotation, i.e recover original axis-aligned features
  Zâ‚Š = Tables.matrix(newtable) * Q'
  
  Zâ‚Š, (Q, qcache)
end

sphering() = Quantile() â†’ EigenAnalysis(:VDV)

function applyfeat(transform::ProjectionPursuit, table, prep) 
  # retrieve column names
  cols = Tables.columns(table)
  names = Tables.columnnames(cols)

  # preprocess the data to approximately spherical shape
  ptable, pcache = apply(sphering(), table)

  # initialize scores and Gaussian quantiles
  Z = Tables.matrix(ptable)
  I = pbasis(transform, Z)
  g = gaussquantiles(transform, size(Z)...) 

  iter = 0; caches = []
  while any(I .> g) && iter â‰¤ transform.maxiter
    # choose direction with maximum projection index
    Î± = alphamax(transform, Z)
    
    # remove non-Gaussian structure
    Z, cache = rmstructure(transform, Z, Î±)
    
    # update the scores along original axes
    I = pbasis(transform, Z)
    
    # store cache and continue
    push!(caches, cache)
    iter += 1
  end

  ð’¯ = (; zip(names, eachcol(Z))...)
  newtable = ð’¯ |> Tables.materializer(table)
  newtable, (pcache, caches)
end

function revertfeat(::ProjectionPursuit, newtable, fcache)
  # retrieve column names
  cols = Tables.columns(newtable)
  names = Tables.columnnames(cols)
  
  # caches to retrieve transform steps
  pcache, caches = fcache

  Z = Tables.matrix(newtable)
  for (Q, qcache) in reverse(caches)
    table = revert(Quantile(1), Tables.table(Z * Q), qcache)
    Z = Tables.matrix(table) * Q'
  end
  
  table = revert(sphering(), Tables.table(Z), pcache)
  Z = Tables.matrix(table)
  
  ð’¯ = (; zip(names, eachcol(Z))...)
  newtable = ð’¯ |> Tables.materializer(newtable)
end
