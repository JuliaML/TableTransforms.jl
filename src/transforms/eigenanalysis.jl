# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    EigenAnalysis(proj)

The eigenanalysis of the covariance with a given projection `proj`.

## Projections

* `:V` - Uncorrelated variables (PCA transform)
* `:VD` - Uncorrelated variables and variance one (DRS transform)
* `:VDV` - Uncorrelated variables and variance one (SDS transformation)

The `:V` projection used in the PCA transform projects the data on the eigenvectors
V of the covariance matrix.

The `:VD` projection used in the DRS transform. Similar to the `:V` projection,
but the eigenvectors are multiplied by the squared inverse of the eigenvalues D.

The `:VDV` projection used in the SDS transform. Similar to the `:VD` transform,
but the data is projected back to the basis of the original variables using the V·µÄ matrix.

See [https://geostatisticslessons.com/lessons/sphereingmaf](https://geostatisticslessons.com/lessons/sphereingmaf)
for more details about these three variants of eigenanalysis.
"""
struct EigenAnalysis <: Transform
  proj::Symbol
end

isrevertible(::Type{EigenAnalysis}) = true

function pcaproj(Œª, V)
  V, transpose(V)
end

function drsproj(Œª, V)
  Œª‚Çõ = sqrt.(Œª)
  Œõ·µ¢ = Diagonal(1 ./ Œª‚Çõ)
  Œõ‚Çõ = Diagonal(Œª‚Çõ)
  S = V * Œõ·µ¢
  S·µ¢ = Œõ‚Çõ * transpose(V)
  S, S·µ¢
end

function sdsproj(Œª, V)
  Œª‚Çõ = sqrt.(Œª)
  Œõ·µ¢ = Diagonal(1 ./ Œª‚Çõ)
  Œõ‚Çõ = Diagonal(Œª‚Çõ)
  S = V * Œõ·µ¢ * transpose(V)
  S·µ¢ = V * Œõ‚Çõ * transpose(V)
  S, S·µ¢
end

function matrices(transform::EigenAnalysis, Œª, V)
  transform.proj == :V && return pcaproj(Œª, V)
  transform.proj == :VD && return drsproj(Œª, V)
  transform.proj == :VDV && return sdsproj(Œª, V)
end

function apply(transform::EigenAnalysis, table)
  @assert transform.proj ‚àà [:V, :VD, :VDV] "eigen analysis not suported"

  # sanity checks
  sch = schema(table)
  names = sch.names
  types = sch.scitypes
  @assert all(T <: Continuous for T in types) "columns must hold continuous variables"

  X = Tables.matrix(table)
  Œº = mean(X, dims=1)
  X = X .- Œº
  Œ£ = cov(X)
  Œª, V = eigen(Œ£)
  Œì, Œì‚Åª¬π = matrices(transform, Œª, V)
  Y = X * Œì

  # table with transformed columns
  ùíØ = (; zip(names, eachcol(Y))...)
  newtable = ùíØ |> Tables.materializer(table)

  newtable, (Œì‚Åª¬π, Œº)
end

function revert(::EigenAnalysis, newtable, cache)
  # sanity checks
  sch = schema(newtable)
  names = sch.names
  types = sch.scitypes
  @assert all(T <: Continuous for T in types) "columns must hold continuous variables"

  Œì‚Åª¬π, Œº = first(cache), last(cache)

  Y = Tables.matrix(newtable)
  X = Y * Œì‚Åª¬π
  X = X .+ Œº

  # table with original columns
  ùíØ = (; zip(names, eachcol(X))...)
  ùíØ |> Tables.materializer(newtable)
end

"""
    PCA()

The PCA transform is a shortcut for
`ZScore() ‚Üí EigenAnalysis(:V)`.
"""
PCA() = ZScore() ‚Üí EigenAnalysis(:V)

"""
    DRS()

The DRS transform is a shortcut for
`ZScore() ‚Üí EigenAnalysis(:VD)`.
"""
DRS() = ZScore() ‚Üí EigenAnalysis(:VD)

"""
    SDS()

The SDS transform is a shortcut for
`ZScore() ‚Üí EigenAnalysis(:VDV)`.
"""
SDS() = ZScore() ‚Üí EigenAnalysis(:VDV)