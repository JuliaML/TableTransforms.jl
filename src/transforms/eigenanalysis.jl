# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    EigenAnalysis(proj)

The eigenanalysis of the covariance with a given projection `proj`.

## Projections

* `:V` - Uncorrelated variables (PCA transform)
* `:VD` - Uncorrelated variables and variance one (DRS transform)
* `:VDV` - Uncorrelated variables and variance one (SDS transformation)

The `:V` projection used in the PCA transform projects the data on the eigenvectors
V of the covariance matrix.

The `:VD` projection used in the DRS transform. Similar to the `:V` projection,
but the eigenvectors are multiplied by the squared inverse of the eigenvalues D.

The `:VDV` projection used in the SDS transform. Similar to the `:VD` transform,
but the data is projected back to the basis of the original variables using the Váµ€ matrix.

See [https://geostatisticslessons.com/lessons/sphereingmaf](https://geostatisticslessons.com/lessons/sphereingmaf)
for more details about these three variants of eigenanalysis.
"""
struct EigenAnalysis <: Transform
  proj::Symbol

  function EigenAnalysis(proj)
    @assert proj âˆˆ (:V, :VD, :VDV) "invalid projection"
    new(proj)
  end
end

assertions(::Type{EigenAnalysis}) = [assert_continuous]

isrevertible(::Type{EigenAnalysis}) = true

function pcaproj(Î», V)
  V, transpose(V)
end

function drsproj(Î», V)
  Î› = Diagonal(sqrt.(Î»))
  S = V * inv(Î›)
  Sâ»Â¹ = Î› * transpose(V)
  S, Sâ»Â¹
end

function sdsproj(Î», V)
  Î› = Diagonal(sqrt.(Î»))
  S = V * inv(Î›) * transpose(V)
  Sâ»Â¹ = V * Î› * transpose(V)
  S, Sâ»Â¹
end

function matrices(transform::EigenAnalysis, Î», V)
  transform.proj == :V && return pcaproj(Î», V)
  transform.proj == :VD && return drsproj(Î», V)
  transform.proj == :VDV && return sdsproj(Î», V)
end

function apply(transform::EigenAnalysis, table)
  # basic checks
  for assertion in assertions(transform)
    assertion(table)
  end

  # original columns names
  names = Tables.columnnames(table)

  X = Tables.matrix(table)
  Î¼ = mean(X, dims=1)
  X = X .- Î¼
  Î£ = cov(X)
  Î», V = eigen(Î£)
  Î“, Î“â»Â¹ = matrices(transform, Î», V)
  Y = X * Î“

  # table with transformed columns
  ð’¯ = (; zip(names, eachcol(Y))...)
  newtable = ð’¯ |> Tables.materializer(table)

  newtable, (Î“â»Â¹, Î¼)
end

function revert(::EigenAnalysis, newtable, cache)
  # transformed column names
  names = Tables.columnnames(newtable)

  Y = Tables.matrix(newtable)
  Î“â»Â¹, Î¼ = cache
  X = Y * Î“â»Â¹
  X = X .+ Î¼

  # table with original columns
  ð’¯ = (; zip(names, eachcol(X))...)
  ð’¯ |> Tables.materializer(newtable)
end

"""
    PCA()

The PCA transform is a shortcut for
`ZScore() â†’ EigenAnalysis(:V)`.
"""
PCA() = ZScore() â†’ EigenAnalysis(:V)

"""
    DRS()

The DRS transform is a shortcut for
`ZScore() â†’ EigenAnalysis(:VD)`.
"""
DRS() = ZScore() â†’ EigenAnalysis(:VD)

"""
    SDS()

The SDS transform is a shortcut for
`ZScore() â†’ EigenAnalysis(:VDV)`.
"""
SDS() = ZScore() â†’ EigenAnalysis(:VDV)